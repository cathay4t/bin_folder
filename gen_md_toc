#!/usr/bin/perl -w
#Copyright (C) 2015  Gris Ge <cnfourt@gmail.com>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

use strict;

my @links;
my %title_level_hash;
my $regex_appendix = qr/
    Appendix\.([A-Z])(?:\.){0,1}
        (?:\.([0-9]+)){0,1}
        (?:\.([0-9]+)){0,1}\ .+
/x;

while (<>) {
    if (m|<h([0-9]+) id="([^"]+)">(.+)</h|) {
        my $level = $1;
        next if $level < 2;

        my $link  = $2;
        my $title = $3;
        $title =~ s|<[/]*code>|`|g;
        my $link_number = '';
        if ( $title =~ /^[0-9]/ ) {
            while ( $title =~ m/([0-9]+)\./g ) {
                $link_number .= sprintf( "%02d", $1 );
            }
        }
        else {
            if ( $title =~ $regex_appendix ) {
                $link_number = sprintf( "a%s", lc($1) );
                $link_number .= sprintf( "%02d", $2 ) if $2;
                $link_number .= sprintf( "%02d", $3 ) if $3;
            }
            else {
                print "FAIL: failed $level title: '$title'\n";
                next;
            }
        }
        unless ( defined $title_level_hash{$level} ) {
            $title_level_hash{$level} = [];
        }
        push @{ $title_level_hash{$level} }, "* [$title][$link_number]";
        push @links, "[$link_number]: #$link";
    }
}

foreach my $link_level ( sort( keys(%title_level_hash) ) ) {
    printf( "%s", join( "\n", @{ $title_level_hash{$link_level} } ) );
    print("\n");
    print("\n");
}

printf( "%s", join( "\n", @links ) );
print("\n");
